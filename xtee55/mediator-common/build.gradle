project.ext.schemaTargetDir = new File("$buildDir/generated-sources")

configurations {
    provided
    xjc
}

sourceSets {
    main {
        java.srcDirs = ['src/main/java', 'build/generated-sources']
    }
}

dependencies {
    // SDSB dependencies
    compile 'ee.cyber.sdsb:common-db:1.0'
    compile 'ee.cyber.sdsb:serverconf:1.0'
    
    // Test dependencies
    testCompile 'ee.cyber.sdsb:common-test:1.0'
    // 'sourceSets' trick seems to help to resolve circular dependencies
    testCompile project(':mediator-common-test').sourceSets.main.output
    testCompile 'org.mockito:mockito-all:1.9.0'

    // Additional 3rd party dependencies
    compile 'org.eclipse.jetty:jetty-server:8.1.7.v20120910'
    compile 'commons-io:commons-io:2.4'
    compile 'org.antlr:ST4:4.0.7'
    
    provided 'org.projectlombok:lombok:1.14.8'
    
    xjc 'com.sun.xml.bind:jaxb-impl:2.2.6'
    xjc 'com.sun.xml.bind:jaxb-xjc:2.2.6'
}

sourceSets.main.compileClasspath += configurations.provided

eclipse {
    classpath {
        plusConfigurations += [configurations.provided]
    }
}

task createDirs () {
    project.ext.schemaTargetDir.mkdirs()
}

task xjc () {
    ant.taskdef(name: 'xjc', classname: 'com.sun.tools.xjc.XJCTask', classpath: configurations.xjc.asPath)

    // Generate classes for identifier-mapper
    ant.xjc(
        destdir: project.ext.schemaTargetDir,
        package: 'ee.cyber.xroad.mediator.identifiermapping',
        schema: 'doc/identifier-mapping.xsd',
        binding: 'doc/identifiers-bindings.xml'
    )
}

xjc.dependsOn createDirs
compileJava.dependsOn xjc
