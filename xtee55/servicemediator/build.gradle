plugins {
  id "eu.appsatori.fatjar" version "0.3"
}

dependencies {
    compile project(':mediator-common')

    // X-Road 6.0 dependencies
    compile 'ee.ria.xroad:common-util:1.0'
    compile 'ee.ria.xroad:common-verifier:1.0'

    // Additional 3rd party dependencies
    compile 'org.eclipse.jetty:jetty-server:8.1.7.v20120910'
    compile 'commons-io:commons-io:2.4'
    compile 'wsdl4j:wsdl4j:1.6.3'

    testCompile project(':mediator-common-test')
    testCompile 'org.hsqldb:hsqldb:2.3.2'
}

fatJarPrepareFiles {
    include '*'
}

fatJar {
    exclude "META-INF/*.SF"
    exclude "META-INF/*.DSA"
    exclude "META-INF/*.RSA"

    manifest {
        attributes 'Main-Class': 'ee.cyber.xroad.mediator.service.Main'
    }
}

jar.enabled = false
build.dependsOn fatJar

task runServiceMediator(type: JavaExec) {
    jvmArgs '-Dxroad.proxy.globalConfDistributor.enabled=false',
        '-Dxroad.proxy.globalConfFile=src/test/resources/globalconf.xml',
        '-Dxtee55.common.identifier-mapping-file=src/test/resources/identifiermapping.xml',
        '-Dxroad.appLog.path=build/log',
        '-Dlogback.configurationFile=src/test/resources/logback-test.xml'

    main = 'ee.cyber.xroad.mediator.service.Main'
    classpath = sourceSets.main.runtimeClasspath
}
