buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'eu.appsatori:gradle-fatjar-plugin:0.2-rc1'
    }
}

apply plugin: 'fatjar'

configurations {
    provided
}

dependencies {
    compile project(':mediator-common')

    // SDSB dependencies
    compile 'ee.cyber.sdsb:common-util:1.0'
    compile 'ee.cyber.sdsb:common-verifier:1.0'

    // Additional 3rd party dependencies
    compile 'org.eclipse.jetty:jetty-server:8.1.7.v20120910'
    compile 'commons-io:commons-io:2.4'
    compile 'wsdl4j:wsdl4j:1.6.3'
        
    provided 'org.projectlombok:lombok:0.12.0'

    testCompile project(':mediator-common-test')
    testCompile 'org.hsqldb:hsqldb:2.3.2'
}

sourceSets.main.compileClasspath += configurations.provided

eclipse {
    classpath {
        plusConfigurations += configurations.provided 
    }
}

fatJarPrepareFiles {
    include '*'
}

fatJar {
    exclude "META-INF/*.SF"
    exclude "META-INF/*.DSA"
    exclude "META-INF/*.RSA"

    manifest {
        attributes 'Main-Class': 'ee.cyber.xroad.mediator.service.Main'
    }
}

jar.enabled = false
build.dependsOn fatJar

task runServiceMediator(type: JavaExec) {
    jvmArgs '-Dee.cyber.sdsb.proxy.globalConfDistributor.enabled=false',
        '-Dee.cyber.sdsb.proxy.globalConfFile=src/test/resources/globalconf.xml',
        '-Dee.cyber.xroad.identifierMappingFile=src/test/resources/identifiermapping.xml',
        '-Dee.cyber.sdsb.appLog.path=build/log',
        '-Dlogback.configurationFile=src/test/resources/logback-test.xml'
    
    main = 'ee.cyber.xroad.mediator.service.Main'
    classpath = sourceSets.main.runtimeClasspath
}
