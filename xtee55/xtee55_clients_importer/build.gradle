plugins {
  id "eu.appsatori.fatjar" version "0.3"
}


project.ext.set("XROAD_HOME", System.getenv('XROAD_HOME'))

dependencies {
    compile project(':mediator-common')
}

fatJarPrepareFiles {
    include '*'
}

fatJar {
    exclude "META-INF/*.SF"
    exclude "META-INF/*.DSA"
    exclude "META-INF/*.RSA"
}

jar.enabled = false

clean << {
    project.delete "lib/xroad",
            "lib/xtee55_clients_importer/xtee55_clients_importer.jar",
            "lib/xtee55_clients_importer/version.rb"
    // project.delete does not support wildcards.
    ant.delete {
        fileset(dir: file('.'), includes: '*.gem')
    }
}

task copyCenterCommon(type: Copy) {
    doFirst {
        if (project.XROAD_HOME == null) {
           throw new GradleException('System environment XROAD_HOME not set')
        }
    }

    into "$projectDir/lib/xroad"
    from project.XROAD_HOME + "/center-common/app/models/"
    include 'common_sql.rb',
        'client_id.rb',
        'distributed_files.rb',
        'identifier.rb',
        'client_id.rb',
        'security_server_id.rb',
        'global_group_member.rb',
        'global_group.rb',
        'member_class.rb',
        'x_road_member.rb',
        'request.rb',
        'request_processing.rb',
        'request_with_processing.rb',
        'deletion_request.rb',
        'client_reg_request.rb',
        'auth_cert_deletion_request.rb',
        'auth_cert_reg_request.rb',
        'client_deletion_request.rb',
        'security_server_client_name.rb',
        'security_server_client.rb',
        'subsystem.rb',
        'system_parameter.rb',
        'validators.rb'
}

task createGemVersion(type: Exec) {
   commandLine = "./create_gem_version.sh $version $projectDir/lib/xtee55_clients_importer/version.rb".tokenize()
}

task copyJar(dependsOn: fatJar, type: Copy) {
   into "$projectDir/lib/xtee55_clients_importer"
   from "$buildDir/libs/xtee55_clients_importer-${version}.jar"
   rename "xtee55_clients_importer-${version}.jar", "xtee55_clients_importer.jar"
}

task buildGem(dependsOn: [createGemVersion, copyCenterCommon, copyJar], type: Exec) {
    commandLine = [
            'jruby', '-S', 'gem', 'build', 'xtee55_clients_importer.gemspec']
}

build.dependsOn fatJar
build.dependsOn buildGem
