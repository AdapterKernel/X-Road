def getRubySourceCheckCommand(def projectPath) {
    return ["$rootDir/check_ruby_source.sh", "$projectPath"]
}

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.11.0'
    }
}

subprojects {
    apply plugin: 'eclipse'
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'idea'
    apply plugin: 'checkstyle'
    apply plugin: 'pmd'
    apply plugin: 'jacoco'
    group = 'ee.ria.xroad'
    version = '1.0'

    jacoco {
        toolVersion = "0.7.7.201606060606"
    }

    repositories {
        mavenCentral()
        mavenLocal()

        // XXX In case mavenCentral is unreachable, use this mirror instead
        // maven {
        //     url "http://mirrors.ibiblio.org/pub/mirrors/maven2"
        // }

        // iaikPkcs11Wrapper is located there
        //maven {
        //    url "http://ground.zero.ee/maven2"
        //}
    }

    configurations {
        provided
    }

    dependencies {
        testCompile 'junit:junit:4.11'

        provided 'org.projectlombok:lombok:1.16.6'

        pmd(
            'net.sourceforge.pmd:pmd-core:5.3.3',
            'net.sourceforge.pmd:pmd-java:5.3.3'
        )
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    tasks.withType(JavaExec) {
        if (project.hasProperty("args")) {
            args = project.getProperty("args").tokenize()
        }
    }

    task buildall(dependsOn: [build, ':proxy-ui:warble', ':center-ui:warble', ':center-service:warble']) {
        description = "Builds all that can be built (even the slow tasks)"
    }

    artifacts {
        archives sourcesJar
    }

    sourceSets.main.compileClasspath += configurations.provided
    sourceSets.test.compileClasspath += configurations.provided

    eclipse {
        classpath {
            plusConfigurations += [configurations.provided]
        }
    }

    idea {
        module {
            scopes.PROVIDED.plus += [configurations.provided]
        }
    }

    checkstyle {
        toolVersion = "6.11.2"
        configFile = new File(rootDir, "doc/checkstyle.xml")
        configProperties.config_loc = "$rootDir/doc"
        ignoreFailures = false
        showViolations = true
    }

    pmd {
        ignoreFailures = true
        toolVersion = '5.3.3'
        ruleSets = [
                "java-basic", 
                "java-imports",
                "java-sunsecure", 
                "java-unusedcode"
                ]
    }

    jacocoTestReport {
        reports {
            xml.enabled true
        }
    }
}

configure(subprojects.findAll { it.file('LICENSE.txt').exists() }) {
   
    apply plugin: "license"
  
    license {
        header project.file('LICENSE.txt')
        include '**/*.java'
        include '**/*.rb'
    }

   jar {
        from project.file('LICENSE.txt')
   }

}

configure(subprojects.findAll { it.file('LICENSE.txt').exists() && it.name.contains('-ui') }) {

    apply plugin: "license"
  
    task licenseFormatUi(type:nl.javadude.gradle.plugins.license.License) {
        source = (fileTree('app') + fileTree('etc') + fileTree('config') + fileTree('test'))
    }
    
    licenseFormat.dependsOn licenseFormatUi
}



apply plugin: 'idea'
apply plugin: 'sonar-runner'
apply plugin: 'jacoco'

sonarRunner {
    sonarProperties {
        property "sonar.host.url", "http://localhost:9000"
        property "sonar.jdbc.url", "jdbc:postgresql://localhost/sonar"
        property "sonar.jdbc.driverClassName", "jdbc:postgresql://localhost/sonar"
        property "sonar.jdbc.username", "sonar"
        property "sonar.jdbc.password", "sonar"
    }
}

repositories {
    jcenter()
}

task clean(type: Delete) {
    delete "${rootDir}/packages/xroad-jetty9/jetty9/"
    delete "${rootDir}/packages/xroad/center/usr/share/xroad/scripts/restore_db.sh"
    delete "${rootDir}/packages/xroad/debian/xroad-common.xroad-signer.upstart"
    delete "${rootDir}/packages/xroad/debian/xroad-monitor.xroad-monitor.upstart"
    delete "${rootDir}/packages/xroad/debian/xroad-proxy.xroad-confclient.upstart"
    delete "${rootDir}/packages/xroad/debian/xroad-proxy.xroad-proxy.upstart"
    delete "${rootDir}/packages/xroad/proxy/usr/share/xroad/scripts/restore_db.sh"

    ant.delete {
        fileset(dir: file("${rootDir}/packages"), includes: "*.deb, *.changes")
    }
}
