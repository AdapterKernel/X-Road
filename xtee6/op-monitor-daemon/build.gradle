plugins {
    id 'com.github.johnrengelman.shadow' version '1.2.3'
}

project.ext.schemaTargetDir = new File("$buildDir/generated-sources")

configurations {
    xjc
    bugfixagent
}

sourceSets {
    main {
        java.srcDirs = ['src/main/java', 'build/generated-sources']
        resources.srcDirs = ['src/main/resources', '../common-util/src/main/resources']
    }

    bugfixagent {
        java.srcDirs = ['src/bugfixagent/java']
    }
}

dependencies {
    compile 'javax.validation:validation-api:1.1.0.Final'
    compile 'io.dropwizard.metrics:metrics-core:3.1.0'

    compile project(':common-db')
    compile project(':common-op-monitoring')
    compile project(':monitoring-conf')

    bugfixagentCompile 'org.javassist:javassist:3.21.0-GA'

    // Using HSQLDB for an in-memory database in tests.
    testCompile 'org.hsqldb:hsqldb:2.3.2'
    testCompile 'commons-cli:commons-cli:1.2'

    xjc 'com.sun.xml.bind:jaxb-impl:2.2.7'
    xjc 'com.sun.xml.bind:jaxb-xjc:2.2.7'
}

task createDirs() {
    project.ext.schemaTargetDir.mkdirs()
}

task xjc() {
    doLast {
        println '---- op-monitor-daemon xjc @ configuration phase'
        ant.taskdef(name: 'xjc',
                classname: 'com.sun.tools.xjc.XJCTask',
                classpath: configurations.xjc.asPath)

        // Generate classes for query operational data and health data messages.
        println '---- op-monitor-daemon xjc @ execution phase'
        ant.xjc(
                destdir: project.ext.schemaTargetDir,
                package: 'ee.ria.xroad.opmonitordaemon.message',
                schema: 'build/resources/main/op-monitoring.xsd',
                binding: 'build/resources/main/identifiers-bindings.xml'
        )
    }
}

shadowJar {
    append('reference.conf')
    classifier = ''
    manifest {
        attributes 'Main-Class': 'ee.ria.xroad.opmonitordaemon.OpMonitorDaemonMain'
    }
}

task testsJar(type: Jar, dependsOn: testClasses) {
    classifier = 'tests'
    manifest {
        attributes 'Main-Class': 'ee.ria.xroad.opmonitordaemon.OperationalDataRecordsGenerator'
    }
    from {
        sourceSets.test.output
    }
}

task bugfixagentJar(type: Jar) {
    archiveName = "metrics-bugfix-agent.jar"
    manifest {
        attributes(
            'Premain-Class': 'ee.ria.xroad.opmonitordaemon.DropwizardBugfixAgent',
            'Can-Redefine-Classes': 'true',
            'Can-Retransform-Classes': 'true',
            'Can-Set-Native-Method-Prefix': 'true',
            'Implementation-Title': "bugfixagent",
            'Implementation-Version': rootProject.version
        )
    }
    from {
        sourceSets.bugfixagent.output.classesDir
    }
}

jar.enabled = false
build.dependsOn shadowJar
build.dependsOn testsJar
buildall.dependsOn shadowJar
buildall.dependsOn testsJar
shadowJar.dependsOn bugfixagentJar

task licenseFormatJava(type:nl.javadude.gradle.plugins.license.License) {
    source = fileTree('src/main/java')
}

task licenseTestJava(type:nl.javadude.gradle.plugins.license.License) {
    source = fileTree('src/main/java')
    check = true
}

licenseFormatMain.enabled = false
licenseMain.enabled = false

licenseFormat.dependsOn licenseFormatJava
licenseTest.dependsOn licenseTestJava

xjc.dependsOn createDirs
xjc.dependsOn processResources
xjc.mustRunAfter processResources

compileJava.dependsOn xjc
compileJava.dependsOn processResources

test {
    //testLogging {
    //    showStandardStreams = true;
    //}

    jacoco {
        append = false
        destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
    }
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.enabled true
    }
}

idea.module {
    excludeDirs -= file(buildDir)
    buildDir.listFiles({ d, f -> f != 'generated-sources' } as FilenameFilter).each { excludeDirs += it }
}
