configurations {
    schema
}

sourceSets {
    main {
        java.srcDirs = ['src/main/java', 'build/generated-sources']
        resources.srcDirs = ['src/main/resources']
    }
}

dependencies {
    compile project(':common-db')
    compile project(':common-verifier')
    compile 'postgresql:postgresql:9.1-901.jdbc4'

    // DB layer tests use HSQLDB with in-memory tables
    testCompile 'org.hsqldb:hsqldb:2.3.2'
    testCompile project(':common-db')
    testCompile project(':common-test')
    testCompile 'org.hibernate:hibernate-c3p0:4.3.4.Final'

    schema project(':common-util')
    schema 'org.hibernate:hibernate-c3p0:4.3.4.Final'
    schema 'org.hibernate:hibernate-tools:4.3.1.CR1'
    schema 'ch.qos.logback:logback-classic:1.0.6'
    schema 'org.hsqldb:hsqldb:2.3.2'
}

task schemaExport () {
    doLast {
        ant.taskdef(name: 'schemaExport', classname: 'org.hibernate.tool.ant.HibernateToolTask', classpath: configurations.schema.asPath)
        
        ant.schemaExport(destdir: 'build') {
            annotationconfiguration(
                configurationfile: 'src/main/resources/hibernate.cfg.xml')
            hbm2ddl(
                export: false,
                outputfilename: 'schema-export.sql',
                delimiter: ';',
                format: 'true')
            classpath {
                pathelement(path: configurations.schema.asPath)
                pathelement(path: 'src/main/resources')
                pathelement(path: 'src/test/resources')
                pathelement(path: 'build/classes/main')
            }
        }
    }
}

task runServerConfCRUDTest (type: JavaExec) {
    jvmArgs '-Dlogback.configurationFile=src/test/resources/logback-crudtest.xml'
    main = 'ee.ria.xroad.proxy.conf.ServerConfCRUDTest'
    classpath = sourceSets.test.runtimeClasspath
    standardInput = System.in;
}

schemaExport.dependsOn compileJava
