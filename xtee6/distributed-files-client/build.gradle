buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'eu.appsatori:gradle-fatjar-plugin:0.2-rc1'
    }
}

apply plugin: 'fatjar'

configurations {
    provided
}

dependencies {
    compile project(':common-util')
    
    provided 'org.projectlombok:lombok:0.12.0'
    
    testCompile project(':common-test')
}

sourceSets.main.compileClasspath += configurations.provided

eclipse {
    classpath {
        plusConfigurations += configurations.provided 
    }
}

fatJarPrepareFiles {
    include '*'
}

fatJar {
    exclude "META-INF/*.SF"
    exclude "META-INF/*.DSA"
    exclude "META-INF/*.RSA"

    manifest {
        attributes 'Main-Class': 'ee.cyber.sdsb.distributedfiles.DistributedFilesClientMain'
    }
}

jar.enabled = false
build.dependsOn fatJar
buildall.dependsOn fatJar

task runDistributedFilesClient(type: JavaExec) {
    jvmArgs '-Dee.cyber.sdsb.proxy.configurationFile=../systemtest/conf/local_test/serverconf_consumer.xml',
            '-Dee.cyber.sdsb.conf.path=build/',
            '-Dee.cyber.sdsb.proxy.globalConfFile=globalconf.xml',
            '-Dee.cyber.sdsb.distributedfiles.signatureFreshness=-1' // disable freshness checking since we read local file
    
    main = 'ee.cyber.sdsb.distributedfiles.DistributedFilesClientMain'
    classpath = sourceSets.main.runtimeClasspath
}